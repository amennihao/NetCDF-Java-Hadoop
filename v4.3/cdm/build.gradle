jar {
    manifest {
        attributes 'Implementation-Title': 'cdm'
    }
}

sourceSets {
    main {
        java {
            exclude 'thredds/catalog2/**/*.java'
            exclude 'ucar/nc2/util/reflect/*.java'
        }
    }
}

dependencies {
  compile project(':udunits')
  compile 'commons-httpclient:commons-httpclient:3.1'
  compile 'com.google.protobuf:protobuf-java:2.4.1'
  compile 'com.sun.jna:jna:3.0.9'
  compile 'com.sleepycat:je:4.0.92'
  compile 'joda-time:joda-time:2.0'
  compile 'net.jcip:jcip-annotations:1.0'
  compile 'net.sf.ehcache:ehcache:1.6.0'
  compile 'org.jdom:jdom:1.1'
  compile ('org.quartz-scheduler:quartz:2.1.5') {   // eliminate c3p0
    transitive = false
  }
  compile 'org.slf4j:slf4j-api:1.6.4'
  testCompile group: 'junit', name: 'junit', version: '4.8.2'
  testCompile 'org.slf4j:slf4j-jdk14:1.6.4'
  runtime 'org.slf4j:jcl-over-slf4j:1.6.4'
}

javadoc {
  title = 'Public javadoc for netcdf-java version ' + version
  source = fileTree(dir: 'src/main/java').matching {
    include 'thredds/catalog/*.java'
    include 'ucar/ma2/*.java'
    include 'ucar/nc2/*.java'
    include 'ucar/nc2/constants/*.java'
    include 'ucar/nc2/dataset/**/*.java'
    include 'ucar/nc2/dt/*.java'
    include 'ucar/nc2/dt/grid/*.java'
    include 'ucar/nc2/ft/*.java'
    include 'ucar/nc2/ncml/*.java'
    include 'ucar/nc2/time/*.java'
    include 'ucar/nc2/units/*.java'
    include 'ucar/nc2/util/*.java'
    include 'ucar/unidata/**/*.java'
    include 'ucar/units/*.java'
  }
}

task releaseDocs(dependsOn: javadoc) << {
  releaseDir = webdir + "javadoc"

  ant.delete(dir: releaseDir)
  ant.mkdir(dir: releaseDir)

  copy {
    println "copyDocs to " + releaseDir
    from("build/docs/javadoc")
    into releaseDir
  }
}